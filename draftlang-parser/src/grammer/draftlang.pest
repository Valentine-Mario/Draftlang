WHITESPACE= _{" "| "\t" | "\r"| "\n"}
COMMENT = _{ "#" ~ (!"\n" ~ ANY)* }

//verbs
verbs = {  "|>"| ">"| "<" | "+" |  "&&" | "||" | ">=" | "<="}

reserver_words= {
    (
    "func" | "import" | "from"| "return" | "break" | "in" | 
    "if" | "else" | "nil" | "true" | "false"|"import" | "and" | "or"
    | "draft" | "for"
    )
}

var_name= @{(ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_" | "?")*)}

ident = @{ !(reserver_words ~ !ASCII_ALPHANUMERIC) ~ var_name}
number = @{
    "-"?
    ~ ("0"| ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ ("." ~ ASCII_DIGIT*)?
    ~ (^"e" ~ ("+"|"-")? ~ ASCII_DIGIT+)?
}
string = @{"\"" ~ inner ~ "\""}
inner = @{char*}
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\"| "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4,})
}

single_quote_string= @{"'" ~ single_inner ~ "'"}
single_inner = @{single_quote_char*}
single_quote_char = {
    |!("\'"| "\\") ~ ANY
    | "\\" ~ ("'" | "\\"| "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4,})
}

boolean = {"true"| "false"}
null = {"nil"}
array= {
    "["~"]" | "[" ~ (types | ident ) ~ ("," ~ (types|ident))* ~ ","? ~"]"
}

map_item= {
    (string | single_quote_string) ~ ":" ~ (ident|types)
}
map = {
    "%{"~"}"|"%{" ~ map_item ~ 
    ("," ~ map_item)* ~ ","? ~ "}"
}
module_call = {ident ~ ("::" ~ ident)*}

//valid draflang types
types = { 
    number 
    | string 
    | boolean 
    | array 
    | single_quote_string
    | map
    | null
  }

//var expression
assignment = {ident ~ "=" ~ (types|ident|str_concat|func_call)}

//inline expression
inline_expr = {assignment | func_call | return_statement | for_loop | str_concat}

//string concat
str_concat = { (string|single_quote_string) ~ 
    "+" ~ (string|single_quote_string) ~ 
    ("+" ~ (string|single_quote_string))*}

//import expression
import_stat= {"import" ~ ident ~ ("," ~ ident)* ~ "from" ~  module_call }

//function expression
return_statement = {"return" ~ (ident | types | func_call)}
func_param = {"(" ~ ")" | "(" ~ (ident|types) ~ ("," ~ (ident|types))* ~ ")"}
func_expression = {
        "func" ~ ident ~ 
        func_param ~ "{" ~ (inline_expr|types)* ~ "}"
                }

func_call = {ident ~ func_param ~ ("|>" ~ ident ~ func_param)*}

//for loop
for_loop = {"for" ~ ident ~ "," ~ ident ~ "in" ~ (ident|types) ~ 
        "{" ~ (inline_expr|types)* ~ 
            "}" }

//expressions
expr = {assignment|import_stat|func_expression}
json_object = { 
    "{" ~ "}" | "{" ~ json_pair ~ ("," ~ json_pair)* ~ "}"
}
json_pair= {string ~ ":" ~ string}


draft_lang = {json_object? ~ "[draft]" ~ expr* ~ "[draft]" ~ json_object? }
program= _{ SOI ~ draft_lang ~ EOI}